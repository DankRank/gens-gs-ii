# Source: http://public.kitware.com/Bug/bug_view_page.php?bug_id=10069

SET(ASM_DIALECT "_NASM")
SET(CMAKE_ASM${ASM_DIALECT}_SOURCE_FILE_EXTENSIONS nasm;nas;asm)

# Determine the object format.
IF(WIN32)
	IF(CMAKE_C_SIZEOF_DATA_PTR EQUAL 8)
		SET(CMAKE_ASM${ASM_DIALECT}_OBJECT_FORMAT win64)
	ELSE()
		SET(CMAKE_ASM${ASM_DIALECT}_OBJECT_FORMAT win32)
	ENDIF()
ELSEIF(APPLE)
	IF(CMAKE_C_SIZEOF_DATA_PTR EQUAL 8)
		SET(CMAKE_ASM${ASM_DIALECT}_OBJECT_FORMAT macho64)
	ELSE()
		SET(CMAKE_ASM${ASM_DIALECT}_OBJECT_FORMAT macho)
	ENDIF()
ELSE()
	IF(CMAKE_C_SIZEOF_DATA_PTR EQUAL 8)
		SET(CMAKE_ASM${ASM_DIALECT}_OBJECT_FORMAT elf64)
		SET(CMAKE_ASM${ASM_DIALECT}_DEBUG_FORMAT dwarf)
	ELSE()
		SET(CMAKE_ASM${ASM_DIALECT}_OBJECT_FORMAT elf)
		SET(CMAKE_ASM${ASM_DIALECT}_DEBUG_FORMAT dwarf)
	ENDIF()
ENDIF()

# Enable debugging information.
IF(CMAKE_ASM${ASM_DIALECT}_DEBUG_FORMAT)
	SET(CMAKE_ASM${ASM_DIALECT}_DEBUG_FLAGS "-g -F ${CMAKE_ASM${ASM_DIALECT}_DEBUG_FORMAT}")
ELSE()
	SET(CMAKE_ASM${ASM_DIALECT}_DEBUG_FLAGS "")
ENDIF()

# Check for a release build.
IF(CMAKE_BUILD_TYPE MATCHES ^release$)
	SET(CMAKE_ASM${ASM_DIALECT}_COMPILER_ARG1 "${CMAKE_ASM${ASM_DIALECT}_COMPILER_ARG1} -O3")
ENDIF()

# NASM build command.
SET(CMAKE_ASM${ASM_DIALECT}_COMPILE_OBJECT "<CMAKE_ASM${ASM_DIALECT}_COMPILER> <FLAGS> -f ${CMAKE_ASM${ASM_DIALECT}_OBJECT_FORMAT} ${CMAKE_ASM${ASM_DIALECT}_DEBUG_FLAGS} -o <OBJECT> <SOURCE>")

INCLUDE(CMakeASMInformation)
SET(ASM_DIALECT)
