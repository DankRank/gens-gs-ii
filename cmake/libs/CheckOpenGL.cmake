# Check for OpenGL and GLEW.
IF(NOT DEFINED HAVE_OPENGL)

FIND_PACKAGE(OpenGL REQUIRED)
IF(OPENGL_LIBRARIES)
	# Found OpenGL.
	SET(HAVE_OPENGL 1)
	MESSAGE(STATUS "Found OpenGL: ${OPENGL_gl_LIBRARY}")
	IF(USE_GLEW)
		# Compile with GLEW support.
		IF(NOT USE_INTERNAL_GLEW)
			# Find GLEW.
			SET(OLD_CMAKE_REQUIRED_LIBRARIES ${CMAKE_REQUIRED_LIBRARIES})
			SET(CMAKE_REQUIRED_LIBRARIES ${CMAKE_REQUIRED_LIBRARIES} ${OPENGL_gl_LIBRARY})

			# GLEW shared library has a different name on Win32.
			IF(WIN32)
				SET(CHECK_GLEW_LIBRARY "glew32")
			ELSE(WIN32)
				SET(CHECK_GLEW_LIBRARY "GLEW")
			ENDIF(WIN32)

			CHECK_LIBRARY_EXISTS(${CHECK_GLEW_LIBRARY} glewInit "" HAVE_GLEW)
			IF(HAVE_GLEW)
				SET(GLEW_FOUND 1)
				SET(GLEW_LIBRARY "-l${CHECK_GLEW_LIBRARY}")
			ELSE(HAVE_GLEW)
				SET(HAVE_GLEW 1)
				SET(GLEW_LIBRARY glew)
				SET(GLEW_INCLUDE_DIR "${CMAKE_SOURCE_DIR}/extlib/glew/include/")
				SET(USE_INTERNAL_GLEW 1)
				MESSAGE(STATUS "GLEW library not found; using internal GLEW.")
			ENDIF(HAVE_GLEW)

			UNSET(CHECK_GLEW_LIBRARY)
			SET(CMAKE_REQUIRED_LIBRARIES ${OLD_CMAKE_REQUIRED_LIBRARIES})
			UNSET(OLD_CMAKE_REQUIRED_LIBRARIES)
		ELSE(NOT USE_INTERNAL_GLEW)
			# Always use the internal copy of GLEW.
			SET(HAVE_GLEW 1)
			SET(GLEW_LIBRARY glew)
			SET(GLEW_DEFINITIONS -DGLEW_STATIC)
			SET(GLEW_INCLUDE_DIR "${CMAKE_SOURCE_DIR}/extlib/glew/include/")
			SET(USE_INTERNAL_GLEW 1)
			MESSAGE(STATUS "Using internal GLEW library.")
		ENDIF(NOT USE_INTERNAL_GLEW)
	ENDIF(USE_GLEW)
ENDIF(OPENGL_LIBRARIES)

ENDIF(NOT DEFINED HAVE_OPENGL)
