# Platform-specific functionality.

# Hack to remove -rdynamic from CFLAGS and CXXFLAGS
# See http://public.kitware.com/pipermail/cmake/2006-July/010404.html
IF(CMAKE_SYSTEM_NAME STREQUAL "Linux")
	SET(CMAKE_SHARED_LIBRARY_LINK_C_FLAGS)
	SET(CMAKE_SHARED_LIBRARY_LINK_CXX_FLAGS)
ENDIF()

# Don't embed rpaths in the executables.
SET(CMAKE_SKIP_RPATH ON)

# If we're not using x86, disable emulation functionality.
# TODO: Port everything from asm to C/C++!
# TODO: Add configuration option.
INCLUDE(CheckSystemX8632)
CHECK_SYSTEM_X86_32(GENS_CPU_X86_32)
IF(GENS_CPU_X86_32)
	SET(GENS_ENABLE_EMULATION 1)
ENDIF(GENS_CPU_X86_32)
IF(NOT GENS_ENABLE_EMULATION)
	MESSAGE(WARNING "System is not X86_32; CPU emulation will be disabled. (FOR DEBUGGING ONLY)")
ENDIF(NOT GENS_ENABLE_EMULATION)

IF(MSVC)
	INCLUDE(cmake/platform/msvc.cmake)
ELSE(MSVC)
	INCLUDE(cmake/platform/gcc.cmake)
ENDIF(MSVC)

# Platform-specific configuration.
SET(GENS_CFLAGS_PLATFORM "")
SET(GENS_CXXFLAGS_PLATFORM "")
SET(GENS_LDFLAGS_PLATFORM "")
IF(WIN32)
	INCLUDE(cmake/platform/win32.cmake)
ENDIF(WIN32)

# Common CFLAGS/CXXFLAGS for each configuration.
SET(GENS_CFLAGS_CONFIG_COMMON	"${GENS_C99_CFLAG} ${GENS_CFLAGS_COMMON} ${GENS_CFLAGS_PLATFORM}")
SET(GENS_CXXFLAGS_CONFIG_COMMON	"${GENS_CXX11_CXXFLAG} ${GENS_CXXFLAGS_COMMON} ${GENS_CXXFLAGS_PLATFORM} ${GENS_CXX_NO_RTTI_CXXFLAG} ${GENS_CXX_NO_EXCEPTIONS_CXXFLAG}")
SET(GENS_LDFLAGS_CONFIG_COMMON	"${GENS_LDFLAGS_COMMON} ${GENS_LDFLAGS_PLATFORM}")

# Set CFLAGS/CXXFLAGS based on build type.
SET(CMAKE_C_FLAGS_DEBUG			"${GENS_CFLAGS_COMPILER_DEBUG} -DDEBUG ${GENS_CFLAGS_CONFIG_COMMON}")
SET(CMAKE_CXX_FLAGS_DEBUG		"${GENS_CXXFLAGS_COMPILER_DEBUG} -DDEBUG ${GENS_CXXFLAGS_CONFIG_COMMON}")
SET(CMAKE_C_FLAGS_RELEASE		"${GENS_CFLAGS_COMPILER_RELEASE} -DNDEBUG ${GENS_CFLAGS_LTO} ${GENS_CFLAGS_CONFIG_COMMON}")
SET(CMAKE_CXX_FLAGS_RELEASE		"${GENS_CXXFLAGS_COMPILER_RELEASE} -DNDEBUG ${GENS_CFLAGS_LTO} ${GENS_CXXFLAGS_CONFIG_COMMON}")
SET(CMAKE_EXE_LINKER_FLAGS_DEBUG	"${GENS_LDFLAGS_COMPILER_DEBUG} ${GENS_LDFLAGS_CONFIG_COMMON}")
SET(CMAKE_EXE_LINKER_FLAGS_RELEASE	"${GENS_LDFLAGS_COMPILER_RELEASE} ${GENS_LDFLAGS_CONFIG_COMMON} ${GENS_LDFLAGS_LTO}")

# Unset the temporary variables.
UNSET(GENS_C99_CFLAG)
UNSET(GENS_CXX11_COMPAT_HEADER)
UNSET(GENS_CXX11_CXXFLAG)
UNSET(GENS_CFLAGS_COMMON)
UNSET(GENS_CFLAGS_PLATFORM)
UNSET(GENS_LDFLAGS_COMMON)
UNSET(GENS_LDFLAGS_PLATFORM)
UNSET(GENS_CFLAGS_CONFIG_COMMON)
UNSET(GENS_CXXFLAGS_CONFIG_COMMON)
